md = require("markdown")
ctx = context()

if ctx == nil then
  // Server setup
  print("Docs server running at http://localhost:5150")
  server = http_server({port = 5150})
  server.route("GET", "/")
  server.start()
  print("Shutting down server.")
  exit()
end

// Handler code - runs for each request when ctx != nil
req = ctx.request()
path = req.path

// Initialize caching
// Check if dev mode is enabled (disables caching for doc development)
sys_store = datastore("sys")
config = sys_store.get("config")

if config then
  dev_mode = config.docserver_test
end

// Initialize cache (in-memory, flushes on restart)
cache = datastore("docserver")

// Check if path is a binary file (images, fonts)
if contains(path, ~\.(png|jpg|jpeg|gif|webp|ico|woff|woff2|css|js)$~) then
  // Serve as binary file - remove leading / if present
  filepath = replace(path, ~^/~, "")
  exit({
    "status" = 200,
    "filename" = filepath
  })
end

cache_key = path == "" ? "/" : path

if type(req.query) == "array" then
  cache_key = "?" + join(req.query, "+")
elseif req.query then
  cache_key = "?" + req.query
end

if not dev_mode then
  // Check cache (unless dev mode)
  cached_html = cache.get(cache_key)

  if cached_html != nil then
    print("Serving from cache: " + cache_key)
    exit({
      "status" = 200,
      "body" = cached_html,
      "headers" = {"Content-Type" = "text/html"}
    })
  end
end

function smart_load(filename)
//  print(filename)
  var text = ""
  try
    text = load(filename)
  catch(err)
    text = load("/EMBED/" + filename)
  end

  return text
end

content = ""

if path == "/" or path == "" then
  try
    content = smart_load("README.md")
  catch (e)
    content = "Error loading README.md"
  end
elseif path == "/lookup" then
  search_term = req.query.search

  // If search term provided, use doc() to resolve path and redirect
  if search_term != nil and search_term != "" then
    doc_content = doc(search_term)

    // First line is "Documentation from: <path>"
    if doc_content != nil then
      lines = split(doc_content, "\n")
      if len(lines) > 0 then
        first_line = lines[0]

        // Extract path from "Documentation from: <path>"
        source_path = replace(first_line, ~^Documentation from: ~, "")
        if source_path != "" and source_path != first_line then
          exit({
            "status" = 302,
            "headers" = {
              "Location" = "/{{source_path}}"
            }
          })
        end
      end
    end
  end

  var msg = ""
  if search_term then
    msg = "Can't find a doc for `{{search_term}}`. "
  end

  // No search term or couldn't resolve - show search prompt
  content = """
    # Search

    {{msg}} Enter a search term to find documentation for a function, module, or keyword.
  """
else
  // For any other path, remove leading / if present and try to load as a file
  filepath = replace(path, ~^/~, "")
  try
    content = smart_load(filepath)
  catch (e)
    exit({
      "status" = 404,
      "body" = "<h1>404 Not Found</h1><p>" + filepath + "</p>",
      "headers" = {"Content-Type" = "text/html"}
    })
  end
end

// Extract "Documentation from:" line to move to footer
doc_source = ""
content = replace(content, ~^Documentation from: .+\n?~, function(match)
  doc_source = "<small>" + match + "</small>"
  return ""
end)

// Apply markdown formatter
html_content = md.parse(content)

// Build nav bar (only on non-home pages)
nav_html = ""
if path != "/" and path != "" then
  nav_html = """
    <nav class="container">
      <ul>
        <li><a href="https://duso.rocks"><img src="/docs/duso-alt-logo.png" alt="Duso" class="logo"></a></li>
      </ul>
      <ul>
        <li><a href="/">Readme</a></li>
        <li><a href="https://github.com/duso-org/duso">GitHub</a></li>
      </ul>
    </nav>
    """
end

// Wrap in HTML template
html = """
  <!doctype html>
  <html lang="en">
    <head>
      <meta charset="utf-8">
      <meta name="viewport" content="width=device-width, initial-scale=1">
      <meta name="color-scheme" content="light dark">
      <link rel="stylesheet" href="https://cdn.jsdelivr.net/npm/@picocss/pico@2/css/pico.blue.min.css">
      <link rel="stylesheet" href="https://cdn.jsdelivr.net/npm/@picocss/pico@2/css/colors.min.css">
      <link rel="stylesheet" href="/stdlib/docserver/docserver.css">
      <link rel="stylesheet" href="/stdlib/docserver/highlight.css">
      <title>Duso Docs {{path}}</title>
    </head>
    <body>
      <main class="container">
        {{nav_html}}
        {{html_content}}
      </main>
      <footer class="container">
        <i>Search reference docs for any keyword, built-in function name, or module name.</i>
        <form method="get" action="/lookup" role="search">
          <input name="search" type="search" placeholder="Search">
          <input type="submit" value="Search">
        </form>
        {{doc_source}}
        <p>&copy; 2026 Ludonode LLC</p>
      </footer>
      <script src="/stdlib/docserver/prism.js"></script>
      <script src="/stdlib/docserver/prism-duso.js"></script>
      <script>
        Prism.highlightAll();
      </script>
    </body>
  </html>
"""

// Cache the rendered HTML (unless dev mode)
if not dev_mode then
  cache.set(cache_key, html)
end

exit({
  "status" = 200,
  "body" = html,
  "headers" = {"Content-Type" = "text/html"}
})
