// Load markdown formatter before server setup
md = require("md-lite")

ctx = context()

if ctx == nil then
  // Server setup
  print("Docs server running at http://localhost:5150")
  server = http_server({port = 5150})
  server.route("GET", "/")
  server.start()
  print("Shutting down server.")
  exit()
end

// Handler code - runs for each request when ctx != nil
req = ctx.request()
path = req.path

// Check if path is a binary file (images, fonts)
if contains(path, ~\.(png|jpg|jpeg|gif|webp|ico|woff|woff2)$~) then
  // Serve as binary file - remove leading / if present
  filepath = replace(path, ~^/~, "")
  exit({
    "status" = 200,
    "filename" = filepath
  })
end

function smart_load(filename)
//  print(filename)
  var text = ""
  try
    text = load(filename)
  catch(err)
    text = load("/EMBED/" + filename)
  end

  return text
end

content = ""

if path == "/" or path == "" then
  try
    content = smart_load("README.md")
  catch (e)
    content = "Error loading README.md"
  end
else
  // For any other path, remove leading / if present and try to load as a file
  filepath = replace(path, ~^/~, "")
  try
    content = smart_load(filepath)
  catch (e)
    exit({
      "status" = 404,
      "body" = "<h1>404 Not Found</h1><p>" + filepath + "</p>",
      "headers" = {"Content-Type" = "text/html"}
    })
  end
end

// Apply markdown formatter
html_content = md.parse(content)

// Debug: show if parsing happened
// print("Parsed content length: " + len(html_content))

// Wrap in HTML template
html = """
<!doctype html>
<html lang="en">
  <head>
    <meta charset="utf-8">
    <meta name="viewport" content="width=device-width, initial-scale=1">
    <meta name="color-scheme" content="light dark">
    <link rel="stylesheet" href="https://cdn.jsdelivr.net/npm/@picocss/pico@2/css/pico.classless.blue.min.css">
    <title>Duso Docs {{path}}</title>
  </head>
  <body>
    <main class="container">
      {{html_content}}
    </main>
  </body>
</html>
"""

exit({
  "status" = 200,
  "body" = html,
  "headers" = {"Content-Type" = "text/html"}
})
